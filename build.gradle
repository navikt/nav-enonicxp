// Build dependencies
buildscript {
    // Internal dependencies
    if (project.hasProperty('build-inside-firewall')) {
        repositories {
            maven {
                url "https://maven.adeo.no/nexus/content/groups/public/"
            }
            maven {
                url "https://maven.adeo.no/nexus/content/repositories/m2internal/"
            }
        }
        dependencies {
            classpath "gradle.plugin.com.enonic.gradle:xp-gradle-plugin:3.1.0"
        }
    }

    dependencies {
        // Remove this if/when the bug is fixed in the official repo version
        classpath files('xp-codegen-plugin-1.1.9-bugfix.jar')
    }
}

// Build plugins
plugins {
    id 'com.enonic.xp.app' version '3.1.0'
    id "com.moowork.node" version '1.3.1'
    id 'java'
    id 'no.item.xp.codegen' version '1.1.9'
}
node {
    version = '16.14.0'
}
apply plugin: "com.enonic.xp.app"

// App config
app {
    name = project.appName
    displayName = 'Nav.no - XP 2021'
    systemVersion = "${xpVersion}"
}

// Project dependencies
repositories {
    // External dependencies
    if (!project.hasProperty('build-inside-firewall')) {
        mavenLocal()
        mavenCentral()
        xp.enonicRepo()
    }

    mavenLocal()

    maven {
        url "https://maven.adeo.no/nexus/content/groups/public"
    }
}

dependencies {
    implementation "com.enonic.xp:core-api:${xpVersion}"
    implementation "com.enonic.xp:portal-api:${xpVersion}"
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-common:${xpVersion}"
    include "com.enonic.xp:lib-event:${xpVersion}"
    include "com.enonic.xp:lib-auditlog:${xpVersion}"
    include "com.enonic.lib:lib-thymeleaf:2.0.1"
    include "com.enonic.xp:lib-i18n:${xpVersion}"
    include "com.enonic.xp:lib-auth:${xpVersion}"
    include "com.enonic.xp:lib-task:${xpVersion}"
    include "com.enonic.xp:lib-context:${xpVersion}"
    include "com.enonic.xp:lib-io:${xpVersion}"
    include "com.enonic.xp:lib-repo:${xpVersion}"
    include "com.enonic.xp:lib-node:${xpVersion}"
    include "com.enonic.xp:lib-websocket:${xpVersion}"
    include "com.enonic.xp:lib-cluster:${xpVersion}"
    include "com.enonic.xp:lib-scheduler:${xpVersion}"
    include 'com.enonic.lib:lib-urlredirect:3.0.0'
    include "com.enonic.lib:lib-http-client:2.4.0"
    include "com.enonic.lib:lib-cache:2.1.1"
    include "com.enonic.lib:lib-guillotine:5.5.0"
    include "com.enonic.lib:lib-graphql-playground:0.0.1"
    webjar "org.webjars.npm:html-entities:2.1.0"
    webjar "org.webjars.npm:striptags:3.1.1"
}

// Tasks
task babelJs(type: NpmTask, dependsOn: 'npmInstall') {
    dependsOn(generateTypeScript)
    dependsOn(processResources)

    args = ['run', 'build']

    inputs.dir 'src/main/resources'
    outputs.dir "$buildDir/resources/main"
}

jar.dependsOn(babelJs)
processResources.dependsOn(generateTypeScript)
